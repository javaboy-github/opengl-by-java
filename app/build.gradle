import org.gradle.internal.os.OperatingSystem

plugins {
    id 'application'
}

// LWJGL3用
project.ext.lwjglVersion = "3.2.2"
switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        // macのみ、下記のフラグが必要
        applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

repositories {
    mavenCentral()
}

dependencies {
    // LWJGL3
    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
    runtimeOnly "org.lwjgl:lwjgl:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw:$lwjglVersion:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl:$lwjglVersion:$lwjglNatives"


    implementation project(':math')
    // 数学ライブラリ
    implementation 'org.joml:joml:1.10.1'
}

sourceSets {
	main {
		java {
			srcDirs 'src'
		}
	}
}


application {
    mainClass = 'opengl.by.java.Main'
}
